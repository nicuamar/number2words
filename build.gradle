buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
  }
}

plugins {
  id "org.sonarqube" version "2.6"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

group = 'com.mambu.resources'
version = '1.0.3'
def mavenArtifactId = 'number2words'

sourceCompatibility = 1.8

repositories {
	jcenter()
	mavenLocal()
}

// DEPENDENCIES

dependencies {
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'junit:junit:4.12'
}

// CODE COVERAGE
jacoco {
	toolVersion = '0.7.2.201409121644'
	reportsDir = file("$buildDir/reports/")
}

// SOURCE FOLDERS

sourceSets {
	main {
		java {
			srcDir 'src/java'
		}
		resources {
			srcDir 'src/resources'
		}
	}

	test {
		java {
			srcDir 'test/java'
		}
		resources {
			srcDir 'test/resources'
		}
	}
}

// CUSTOM TASKS

task javadocJar (type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task sourceJar (type : Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

// TEST CONFIGURATION

test {
	testLogging {
		// Show that tests are run in the command-line output
		events 'started', 'passed'
	}

	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
		// classDumpFile = file("$buildDir/jacoco/classpathdumps")
	}
}

//
// JACOCO REPORTS CONFIGURATION
// to get the test coverage run "gradle clean test jacoco"
//
jacocoTestReport {
	group = 'reporting'
	description = 'Generate Jacoco coverage reports after running tests.'
	reports {
		xml.enabled true
		html.enabled true
		csv.enabled false
	}
}

// MAVEN PUBLISHING CONFIGURATION

publishing {
	publications {
		mavenJava(MavenPublication) {

			// pom.xml details

			groupId group
			artifactId mavenArtifactId
			version version

			// publish project's jar archive
			from components.java

			// publish project's javadoc jar
			artifact (javadocJar) {
				classifier = 'javadoc'
			}

			// publish project's source jar
			artifact (sourceJar) {
				classifier = 'sources'
			}

		}
	}

	// publish artifact to local directory
	// can change this to publish to a local or central rep
	repositories {
		maven {
			url "file:/$buildDir/artifacts"
		}
	}
}

// ASSEMBLE configuration

jar {
	// MANIFEST.MF details
	manifest {
		attributes 'Implementation-Title': 'number2words',
				   'Implementation-Version': version
	}
}

javadocJar {
	// MANIFEST.MF details
	manifest {
		attributes 'Implementation-Title': 'number2words',
				   'Implementation-Version': version
	}
}

sourceJar {
	// MANIFEST.MF details
	manifest {
		attributes 'Implementation-Title': 'number2words',
				   'Implementation-Version': version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

wrapper {
	gradleVersion = '4.9'
}